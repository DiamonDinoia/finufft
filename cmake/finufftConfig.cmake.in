@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

# Build-time feature flags baked into the config
set(FINUFFT_USE_OPENMP @FINUFFT_USE_OPENMP@)
set(FINUFFT_USE_DUCC0  @FINUFFT_USE_DUCC0@)
set(FINUFFT_USE_CUDA   @FINUFFT_USE_CUDA@)
set(FINUFFT_WAS_BUILT_SHARED @BUILD_SHARED_LIBS@)

# Dependencies required by public interfaces
# Always resolve OpenMP/FFTW for consumers if FINUFFT was built with them,
# regardless of static/shared, so transitive links are available.
if(FINUFFT_USE_OPENMP)
  find_dependency(OpenMP)
endif()

if(NOT FINUFFT_USE_DUCC0)
  find_dependency(FFTW3)
endif()

if(FINUFFT_USE_CUDA)
  find_dependency(CUDAToolkit)
endif()

# Exported targets
include("${CMAKE_CURRENT_LIST_DIR}/finufftTargets.cmake")

# Recreate convenience alias for the build-only FFTW interface if present
if(TARGET finufft_fftw AND NOT TARGET finufft::fftw)
  add_library(finufft::fftw ALIAS finufft_fftw)
endif()
